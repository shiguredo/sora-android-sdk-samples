apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext.kotlin_version = '1.4.31'
    ext.sora_android_sdk_version = '2021.1.1'

    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"

        classpath "com.github.ben-manes:gradle-versions-plugin:0.38.0"
    }

    // デバッグ用: true に設定すると wss ではなく ws で接続できる
    ext.usesCleartextTraffic = false
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
