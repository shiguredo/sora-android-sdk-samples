apply plugin: 'com.android.application'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "jp.shiguredo.sora.sample"
        minSdkVersion 21
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // アプリで参照する設定を BuildConfig に書き込む。
        // gradle.properties に endpoint/channel 設定がある場合は上書きする
        def endpoint   = findProperty("sora.signaling_endpoint") ?: signaling_endpoint
        def channel_id = findProperty("sora.channel_id")         ?: channel_id
        buildConfigField("String", "SIGNALING_ENDPOINT", "\"${endpoint}\"")
        buildConfigField("String", "CHANNEL_ID",         "\"${channel_id}\"")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    flavorDimensions "tier"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        free {
            dimension "tier"
            versionNameSuffix "-free"
        }
    }
}

configurations {
    // assembleRelease などで次のエラーが発生するワークアラウンド
    // "Multiple dex files define Lorg/intellij/lang/annotations/Identifier"
    compile.exclude group: 'org.jetbrains', module: 'annotations'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"

    ext.support_version = '27.0.2'
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.wefika:flowlayout:0.4.1"
    implementation 'com.jaredrummler:material-spinner:1.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Anko libs
    ext.anko_version = '0.9.1'
    implementation "org.jetbrains.anko:anko-sdk19:$anko_version"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"
    implementation "org.jetbrains.anko:anko-cardview-v7:$anko_version"
    implementation "org.jetbrains.anko:anko-recyclerview-v7:$anko_version"

    ext.permissionsdispatcher_version = '3.2.0'
    implementation ("com.github.hotchemi:permissionsdispatcher:${permissionsdispatcher_version}") {
        exclude module: "support-v13"
    }
    kapt "com.github.hotchemi:permissionsdispatcher-processor:${permissionsdispatcher_version}"

    implementation project(path: ':webrtc-video-effector')
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
