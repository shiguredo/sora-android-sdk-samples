apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion 34
    defaultConfig {
        applicationId "jp.shiguredo.sora.sample"
        targetSdkVersion 33
        minSdkVersion 26
        versionCode 1
        versionName "1.0"

        // アプリで参照する設定を BuildConfig / resource に書き込む。
        buildConfigField("String", "SIGNALING_ENDPOINT", "\"${signaling_endpoint}\"")
        resValue("string", "channelId", "\"${channel_id}\"")
        buildConfigField("String", "SIGNALING_METADATA", "\"${signaling_metadata}\"")

        // プロキシの設定
        buildConfigField("String", "PROXY_AGENT", "\"${proxy_agent}\"")
        buildConfigField("String", "PROXY_HOSTNAME", "\"${proxy_hostname}\"")
        buildConfigField("String", "PROXY_PORT", "\"${proxy_port}\"")
        buildConfigField("String", "PROXY_USERNAME", "\"${proxy_username}\"")
        buildConfigField("String", "PROXY_PASSWORD", "\"${proxy_password}\"")

        manifestPlaceholders = [usesCleartextTraffic: usesCleartextTraffic];
    }

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    flavorDimensions "tier"

    buildFeatures {
        compose true

        // AGP 8.0 からデフォルトで false になった
        // このオプションが true でないと、defaultConfig に含まれている
        // buildConfigField オプションが無効になってしまうため、true に設定する
        // 参考: https://developer.android.com/build/releases/past-releases/agp-8-0-0-release-notes#default-changes
        buildConfig true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.15'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        free {
            dimension "tier"
            versionNameSuffix "-free"
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    // AGP 8.0 からモジュールレベルの build script 内に namespace が必要になった
    // 参考: https://developer.android.com/build/releases/past-releases/agp-8-0-0-release-notes#namespace-dsl
    namespace 'jp.shiguredo.sora.sample'
}

configurations {
    // assembleRelease などで次のエラーが発生するワークアラウンド
    // "Multiple dex files define Lorg/intellij/lang/annotations/Identifier"
    compile.exclude group: 'org.jetbrains', module: 'annotations'
}

ktlint {
    version = "0.45.2"
    android = false
    outputToConsole = true
    reporters {
        reporter "checkstyle"
    }
    ignoreFailures = false
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"

    implementation 'com.google.code.gson:gson:2.11.0'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.jaredrummler:material-spinner:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'
    implementation 'androidx.compose.ui:ui:1.6.8'
    implementation 'androidx.compose.material:material:1.6.8'
    implementation 'androidx.compose.material:material-icons-extended:1.6.8'
    implementation 'androidx.activity:activity-compose:1.9.1'

    ext.pd_version = '4.9.2'
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:${pd_version}"
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:${pd_version}"

    implementation project(path: ':webrtc-video-effector')
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    finalizedBy("ktlintFormat")
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
