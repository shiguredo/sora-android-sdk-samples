apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "jp.shiguredo.sora.sample"
        targetSdkVersion 30
        minSdkVersion 26
        versionCode 1
        versionName "1.0"

        // アプリで参照する設定を BuildConfig / resource に書き込む。
        buildConfigField("String", "SIGNALING_ENDPOINT", "\"${signaling_endpoint}\"")
        resValue("string", "channelId", "\"${channel_id}\"")
        buildConfigField("String", "SIGNALING_METADATA", "\"${signaling_metadata}\"")

        manifestPlaceholders = [usesCleartextTraffic: usesCleartextTraffic];
    }

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    flavorDimensions "tier"

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        free {
            dimension "tier"
            versionNameSuffix "-free"
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

configurations {
    // assembleRelease などで次のエラーが発生するワークアラウンド
    // "Multiple dex files define Lorg/intellij/lang/annotations/Identifier"
    compile.exclude group: 'org.jetbrains', module: 'annotations'
}

ktlint {
    version = "0.43.2"
    android = false
    outputToConsole = true
    reporters {
        reporter "checkstyle"
    }
    ignoreFailures = false
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"

    implementation 'com.google.code.gson:gson:2.8.9'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.jaredrummler:material-spinner:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.compose.ui:ui:1.0.3'
    implementation 'androidx.compose.material:material:1.0.3'
    implementation "androidx.compose.material:material-icons-extended:1.0.3"
    implementation 'androidx.activity:activity-compose:1.3.1'

    ext.pd_version = '4.9.1'
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:${pd_version}"
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:${pd_version}"

    implementation project(path: ':webrtc-video-effector')
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    finalizedBy("ktlintFormat")
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
