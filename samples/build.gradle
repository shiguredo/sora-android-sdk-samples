apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "jp.shiguredo.sora.sample"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // アプリで参照する設定を BuildConfig に書き込む。
        // gradle.properties に endpoint/channel 設定がある場合は上書きする
        def endpoint   = findProperty("sora.signaling_endpoint") ?: signaling_endpoint
        buildConfigField("String", "SIGNALING_ENDPOINT", "\"${endpoint}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        free {
            versionNameSuffix "-free"
        }
    }


}

configurations {
    // assembleRelease などで次のエラーが発生するワークアラウンド
    // "Multiple dex files define Lorg/intellij/lang/annotations/Identifier"
    compile.exclude group: 'org.jetbrains', module: 'annotations'
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    ext.support_version = '25.3.1'
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.wefika:flowlayout:0.4.1"
    compile 'com.jaredrummler:material-spinner:1.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Anko libs
    ext.anko_version = '0.9.1'
    compile "org.jetbrains.anko:anko-sdk19:$anko_version"
    compile "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
    compile "org.jetbrains.anko:anko-design:$anko_version"
    compile "org.jetbrains.anko:anko-cardview-v7:$anko_version"
    compile "org.jetbrains.anko:anko-recyclerview-v7:$anko_version"

    compile 'com.github.hotchemi:permissionsdispatcher:2.4.0'
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'

    // Sora Android SDK
    compile("com.github.shiguredo:sora-android-sdk:$sora_android_sdk_version:release@aar") {
        transitive = true
    }

    releaseCompile project(path: ':webrtc-video-effector', configuration: 'release')
    debugCompile project(path: ':webrtc-video-effector', configuration: 'debug')
}

repositories {
    mavenCentral()
}

kapt {
    generateStubs = true
}
